#!/usr/bin/perl

use strict;
use feature 'switch';
use Foundation;
use POSIX;
use YAML::Syck; 
use Getopt::Long;
use Pod::Usage;

$YAML::Syck::ImplicitTyping = 1;

# PerlObjCBridge::setTracing(1);

# Load the Scripting-Bridge
@NSMutableDictionary::ISA = @NSDictionary::ISA = @SBApplication::ISA = qw(PerlObjCBridge);

NSBundle->bundleWithPath_(
    '/System/Library/Frameworks/ScriptingBridge.framework' # Loads AppKit too
)->load;

my $systemevents = SBApplication->applicationWithBundleIdentifier_("com.apple.systemevents");
my $itermApp = SBApplication->applicationWithBundleIdentifier_("com.googlecode.iterm2");
my $itermPredicate = NSPredicate->predicateWithFormat_("bundleIdentifier == 'com.googlecode.iterm2'");
my $itermProcess = $systemevents->processes()->filteredArrayUsingPredicate_($itermPredicate)->firstObject();

# =================================================================================================================
package SshEndpoint;
sub new {
	my $class = shift;
	my $self = {
		_user => shift,
		_host => shift
	};
	bless $self, $class;
	$self;
}

sub command {
	my $self = shift;

	sprintf("ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -l %s %s", $self->{_user}, $self->{_host});
}

# =================================================================================================================
package Cluster;
sub list {
	my $file = shift;
	my %clusters = %{YAML::Syck::LoadFile($file)};

	listKeys("", \%clusters);
}

sub listKeys {
	my $prefix = shift;
	my $clusters_ref = shift;

	while((my $key, my $value) = each(%$clusters_ref)) {
		if ($value->{hosts}) {
			print $prefix . $key . " ";
		} elsif (ref $value eq ref {}) {
			listKeys($prefix . $key . "/", $value);
		}
	}
}

sub new {
	my $class = shift;
	my $self = {
		_endpoints => []
	};
	bless $self, $class;
	$self;
}

sub count {
	my $self = shift;

	scalar @{$self->{_endpoints}};
}

sub rows {
	my $self = shift;

	POSIX::ceil($self->count / $self->cols);
}

sub cols {
	my $self = shift;

	POSIX::ceil(sqrt($self->count));
}

sub sshCommands {
	my $self = shift;

	map { $_->command } @{$self->{_endpoints}};
}

sub addEndpoint {
	my $self = shift;
	my $user = shift;
	my $host = shift;

	push(@{$self->{_endpoints}},SshEndpoint->new($user, $host ));
}

sub addFromConfig {
	my $self = shift;
	my $clusters_conf = shift;
	my $match = shift;

	$self->filterConfig("", $clusters_conf, $match);
}

sub filterConfig {
	my $self = shift;
	my $prefix = shift;
	my $clusters_conf = shift;
	my $match = shift;

	while((my $key, my $value) = each(%$clusters_conf)) {
		if ($value->{hosts}) {
			if (index($prefix . $key, $match) == 0) {
				push(@{$self->{_endpoints}}, map { SshEndpoint->new($value->{user}, $_) } @{$value->{hosts}});
			}
		} elsif (ref $value eq ref {}) {
			$self->filterConfig($prefix . $key . "/", $value, $match);
		}
	}
}

# =================================================================================================================
# This will be used for the current release version of iTerm (2.0.0.x)
package Iterm;
sub new {
	my $class = shift;
	my $self = {
	};
	bless $self, $class;
	$self;
}

sub openCluster {
	my ($self, $cluster) = @_;

	$itermProcess->menuBars()->count() > 0 || die "UI automation disabled or disallowed";

	my $terminal = $itermApp->classForScriptingClass_("terminal")->alloc()->init();

	$itermApp->terminals()->addObject_($terminal);
	my $session = $terminal->launchSession_("Default session");
	my $shellMenu = $itermProcess->menuBars()->firstObject()->menuBarItems()->objectAtIndex_(2)->menus()->firstObject()->menuItems();
	my $windowMenu = $itermProcess->menuBars()->firstObject()->menuBarItems()->objectAtIndex_(7)->menus()->firstObject()->menuItems();
	my $selectSplitPaneMenu = $windowMenu->objectAtIndex_(15)->menus()->firstObject()->menuItems();
	my $selectNextPane = $selectSplitPaneMenu->objectAtIndex_(5);
	my $splitVert = $shellMenu->objectAtIndex_(4);
	my $splitHor = $shellMenu->objectAtIndex_(5);
	my $broadcastInput = $shellMenu->objectAtIndex_(16)->menus()->firstObject()->menuItems()->objectAtIndex_(2);
	my $rows = $cluster->rows;
	my $cols = $cluster->cols;

	if ( $rows > 1 ) {
		for(my $row = 1; $row < $rows; $row++) {
			$splitHor->clickAt_(undef);
		}
		$selectNextPane->clickAt_(undef);
	}
	my $i = 0;
	for ( my $row = 0; $row < $rows; $row++ ) {
		$i++;
		for ( my $col = 1; $col < $cols; $col++ ) {
			if ( $i >= $cluster->count ) {
				last;
			}
			$splitVert->clickAt_(undef);
			$i++;
		}
		$selectNextPane->clickAt_(undef);		
	}
	my $sessions = $terminal->sessions();
	my $count = $sessions->count();
	my @sshCommands = $cluster->sshCommands;

	for ( my $i = 0; $i < $count && $i < $cluster->count; $i++ ) {
		$sessions->objectAtIndex_($i)->writeContentsOfFile_text_(undef, @sshCommands[$i]);
	}

	if ( $count > 1 ) {
		$broadcastInput->clickAt_(undef);
	}
}

# =================================================================================================================
# This will be used for the development version of iTerm (>=2.9)
package Iterm2;
sub new {
	my $class = shift;
	my $self = {
	};
	bless $self, $class;
	$self;
}

sub openCluster {
	my ($self, $cluster) = @_;

	$itermApp->createWindowWithDefaultProfileCommand_(undef);
	my $tab = $itermApp->currentWindow()->currentTab();
	my $session = $tab->currentSession();
	my $rows = $cluster->rows;
	my $cols = $cluster->cols;

	if ( $rows > 1 ) {
		for(my $row = 1; $row < $rows; $row++) {
			$session->splitHorizontallyWithDefaultProfile();
		}
	}
	my $i = 0;
	$tab->sessions()->firstObject()->select();
	for ( my $row = 0; $row < $rows; $row++ ) {
		$i++;
		for ( my $col = 1; $col < $cols; $col++ ) {
			if ( $i >= $cluster->count ) {
				last;
			}			
			$tab->currentSession()->splitVerticallyWithSameProfile();
			$i++;
		}
		$tab->sessions()->lastObject()->select();
	}
	my $sessions = $tab->sessions();
	my $count = $sessions->count();
	my @sshCommands = $cluster->sshCommands;

	for ( my $i = 0; $i < $count && $i < $cluster->count; $i++ ) {
		$sessions->objectAtIndex_($i)->writeContentsOfFile_text_newline_(undef, @sshCommands[$i], 1);
	}

	if ( $count > 1 ) {
		if ( $itermProcess->menuBars()->count() > 0 ) {
			my $shellMenu = $itermProcess->menuBars()->firstObject()->menuBarItems()->objectWithName_("Shell")->menus()->firstObject()->menuItems();
			my $broadcastInput = $shellMenu->objectWithName_("Broadcast Input")->menus()->firstObject()->menuItems()->objectAtIndex_(2);

			$broadcastInput->clickAt_(undef);
		} else {
			print "Warning: UI automation disabled or disallowed";
		}
	}
}

# =================================================================================================================
package main;

my $clusters_file = $ENV{'HOME'} . '/.clusters.yaml';
my %clusters_conf = {};
my $clusters_conf_loaded = 0;
my $cluster = Cluster->new();

my $man = 0;
my $help = 0;
my $edit = 0;
my $list = 0;

sub addCluster {
	my $arg = shift;

	if (!$clusters_conf_loaded) {
		if (-e $clusters_file) {
			%clusters_conf = %{YAML::Syck::LoadFile($clusters_file)};	
		} else {
			print "Warning: No cluster configuration found at $clusters_file\n";
		}
		$clusters_conf_loaded = 1;
	}
	if ($arg =~ /([^@]+)@([^@]+)/) {
		$cluster->addEndpoint($1, $2);
	} else {
		$cluster->addFromConfig(\%clusters_conf, $arg);		
	}
}

GetOptions(
	help => \$help, 
	man => \$man,
	list => \$list,
	edit => \$edit,
	'<>' => \&addCluster) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

if ($edit) {
	my $editor = $ENV{'EDITOR'} || "vi";
	system(sprintf("%s '%s'", $editor, $clusters_file));
	exit 0;
} elsif ($list) {
	Cluster::list($clusters_file);
	exit 0;
}

if ($cluster->count == 0) {
	print "At least one ssh endpoint should be defined\n";
	exit 1;
}

my $iterm;

if ($itermApp->className()->cString() eq "ITerm2Application" ) {
	$iterm = Iterm2->new();
} else {
	$iterm = Iterm->new();
}

$iterm->openCluster($cluster);

__END__

=head1 NAME

csshi2 - Simple cluster ssh for iTerm2

=head1 SYNOPSIS

csshi2 [options] [cluster | user@host ...]

Options:
-help brief help message

-man full documentation

-edit edit the cluster file (ussing the $EDITOR)

-list list all configured clusters

=head1 OPTIONS

=over 8

=item B<-help>
Print a brief help message and exits.

=item B<-man>
Prints the manual page and exits.

=item B<-edit>
Edit the cluster file using the $EDITOR.

=item B<-list>
List all configured clusters.

=back

=head1 DESCRIPTION

B<csshi2> opens multiple ssh connections to one or more clusters of
hosts useing iTerm2. All ssh sessions are opened in a separate pane.

Ssh endpoints can be either defined via command line "user@host" or via a 
.clusters.yaml file in your home directory.

=cut
